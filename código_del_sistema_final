#include <Wire.h>
#include <SD.h>
#include <SPI.h>

#define MPU_ADDR 0x69
#define RTC_ADDR 0x68
#define SD_CS_PIN 2    //#define SD_CS_PIN 5 (esp 38 pins) // #define SD_CS_PIN 2 (esp 30 pins)
#define PIN_BOTON 27
#define PIN_SALIDA 13


const uint8_t MUESTRAS_POR_BLOQUE = 41;
const uint16_t TAMANO_BLOQUE = 512;

uint8_t buffers[4][TAMANO_BLOQUE];
volatile bool buffer_lleno[4] = {false, false, false, false};
volatile uint8_t buffer_activo_idx = 0;

TaskHandle_t taskAdquisicion;
TaskHandle_t taskGuardar;

File archivo;

void setup() {
  Serial.begin(115200);
  delay(1000);

  Wire.begin(21, 22); 

  iniciarMPU();
  configurarMPU();
  iniciarRTC();
  pinMode(PIN_BOTON, INPUT);                 
  pinMode(PIN_SALIDA, OUTPUT);

  if (!SD.begin(SD_CS_PIN)) {
    Serial.println("Error al inicializar SD");
    while (true) vTaskDelay(1000 / portTICK_PERIOD_MS);
  }

  archivo = SD.open("/datos_PIN_4BUF.bin", FILE_WRITE);
  if (!archivo) {
    Serial.println("No se pudo abrir archivo");
    while (true) vTaskDelay(1000 / portTICK_PERIOD_MS);
  }

  xTaskCreatePinnedToCore(adquisicionTask, "Adquisicion", 4096, NULL, 2, &taskAdquisicion, 0);
  xTaskCreatePinnedToCore(guardarTask, "Guardar", 4096, NULL, 1, &taskGuardar, 1);
  while (digitalRead(PIN_BOTON) == LOW) {   
  delay(1);
  }
  Serial.print("Sistema Funcionando");
  digitalWrite(PIN_SALIDA, HIGH);
}

void loop() {
  vTaskDelay(1000 / portTICK_PERIOD_MS);
}

void adquisicionTask(void* pvParameters) {
  while (true) {
    if (buffer_lleno[0] && buffer_lleno[1] && buffer_lleno[2] && buffer_lleno[3]) {
      Serial.println("Todos los buffers llenos, esperando SD.");
      vTaskDelay(10);
      continue;
    }

    if (!buffer_lleno[buffer_activo_idx]) {
      recolectarDatos(buffers[buffer_activo_idx]);
      buffer_lleno[buffer_activo_idx] = true;

      do {
        buffer_activo_idx = (buffer_activo_idx + 1) % 4;
      } while (buffer_lleno[buffer_activo_idx]);
    }

    vTaskDelay(1);
  }
}

void guardarTask(void* pvParameters) {
  while (true) {
    for (uint8_t i = 0; i < 4; i++) {
      if (buffer_lleno[i]) {
        if (!archivo) {
          Serial.println("Archivo SD invÃ¡lido");
        }

        archivo.write(buffers[i], TAMANO_BLOQUE);
        archivo.flush();

        buffer_lleno[i] = false;

        vTaskDelay(1);
      }
    }
    vTaskDelay(1);
  }
}

void recolectarDatos(uint8_t* buffer) {
  uint16_t indice_buffer = 0;
  uint8_t contador_muestras = 0;
  uint8_t timestamp_count = 0;

  while (contador_muestras < MUESTRAS_POR_BLOQUE) {
    int16_t datos[6];
    if (!leerMPU(datos)) {
      Serial.println("Error leyendo MPU");
      continue;
    }

    for (uint8_t i = 0; i < 6; i++) {
      buffer[indice_buffer++] = datos[i] >> 8;
      buffer[indice_buffer++] = datos[i] & 0xFF;
    }

    contador_muestras++;

    if (contador_muestras == 6 || contador_muestras == 12 ||
        contador_muestras == 18 || contador_muestras == 24 ||
        contador_muestras == 30 || contador_muestras == 36) {
      uint8_t hora[3];
      if (!leerHora(hora)) {
        Serial.println("Error leyendo RTC");
        continue;
      }

      for (uint8_t i = 0; i < 3; i++) {
        buffer[492 + timestamp_count * 3 + i] = hora[i];
      }
      timestamp_count++;
    }

    vTaskDelay(1);
  }

  buffer[510] = 0x00;
  buffer[511] = 0x00;
}

bool leerMPU(int16_t* datos) {
  Wire.beginTransmission(MPU_ADDR);
  Wire.write(0x3B);
  if (Wire.endTransmission(false) != 0) return false;
  Wire.requestFrom(MPU_ADDR, 14);
  if (Wire.available() < 14) return false;

  for (uint8_t i = 0; i < 3; i++) datos[i] = (Wire.read() << 8) | Wire.read();
  Wire.read(); Wire.read(); 
  for (uint8_t i = 3; i < 6; i++) datos[i] = (Wire.read() << 8) | Wire.read();

  return true;
}

bool leerHora(uint8_t* hora) {
  Wire.beginTransmission(RTC_ADDR);
  Wire.write(0x00);
  if (Wire.endTransmission() != 0) return false;
  Wire.requestFrom(RTC_ADDR, 3);
  if (Wire.available() < 3) return false;

  for (uint8_t i = 0; i < 3; i++) hora[i] = Wire.read();
  return true;
}

void iniciarMPU() {
  Wire.beginTransmission(MPU_ADDR);
  Wire.write(0x6B);
  Wire.write(0x00);
  Wire.endTransmission();
}

void configurarMPU() {
  writeRegister(0x1B, 0x08);
  writeRegister(0x1C, 0x10);
}

void iniciarRTC() {
  Wire.beginTransmission(RTC_ADDR);
  Wire.endTransmission();
}

void writeRegister(uint8_t reg, uint8_t val) {
  Wire.beginTransmission(MPU_ADDR);
  Wire.write(reg);
  Wire.write(val);
  Wire.endTransmission();
}
